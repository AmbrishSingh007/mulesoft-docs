= To Import DataWeave Modules and Mappings
:keywords: studio, anypoint, esb, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

In DataWeave code, you can import additional components to extend the available variables and functions. This can include additional modules that are packaged with Mule, such as the `dw::Crypto` module, or custom-built modules that can contain any predefined functions you want.

== Importing and Reusing Standard DataWeave Modules
Here is an example of DataWeave code that imports the standard `dw::Strings` module, then uses the capitalize function from the `dw::Strings` module:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
output application/json

import dw::core::Strings
---
(Strings::dasherize ( (payload pluck $$)[0] as String) ) : Strings::capitalize( (payload pluck $)[0] )
----

If the payload passed to this DataWeave expression is:

.Example: Input Payload
[source,DataWeave,linenums]
----
{
    "some key": "bar"
}
----

The output of the DataWeave expression above:

.Example: Output
[source,DataWeave,linenums]
----
{
  "some-key": "Bar"
}
----

== Writing, Importing, and Reusing Custom DataWeave Modules
You can store DataWeave functions and variables in external module files, sometimes called _mapping files_, then import them into another DataWeave script so that you can use the custom function or variable in that script. For example, you might import it into a Transform component in a Mule app flow.

. Create your module, for example:
+
.Example: DataWeave Module (Mapping File Content)
[source,DataWeave,linenums]
----
%dw 2.0
fun myfunc(name:String) = name ++ "_"
----
+
Unlike a DataWeave script, the module cannot contain an output directive, body expression, or the separator `---` between header and body sections.
+
The custom module file can only contain `var`, `fun`, and `ns` declarations.
+
. Save your function module in a `.dwl` file, such as `myfunc.dwl`.
. In a Studio project, store the file in a subfolder that you create under `src/main/resources`, such as `src/main/resources/modules`.
+
Saving the module to this location makes it accessible for use by a component in a Mule app in that project.

== Creating, Importing, and Reusing a DataWeave Mapping File as a Function
You can use the `import` directive to import any valid DataWeave (`.dwl`) file into a Mule component that supports DataWeave transformations (such as the Transform component). Here is sample content for a DataWeave mapping file:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
import dw::core::String

fun capitalizeKey(value:String) = Strings::capitalize(value) ++ "Key"
---
{
  capitalizeKey( (payload pluck $$)[0] as String ) : (payload pluck $)[0]
}
----

Assume that you store the DataWeave mapping file above in `MyMapping.dwl` under a  Mule project subdirectory (`src/main/resources/modules`).

Here is an example of importing and reusing the body expression from this DataWeave Mapping file.

.Example
[source,DataWeave,linenums]
----
%dw 2.0
import modules::MyMapping
output application/json
---
MyMapping::main({"payload": {user: "bar"}}
----

When you import a DataWeave mapping file, its body is considered to be an additional function named `main`. This function expects an input that follows the structure of the input that the transformation uses.

Here is the result of this DataWeave mapping:

.Output
[source,Json,linenums]
----
{
    "UserKey": "bar"
}
----

Notice that even though the capitalizeKey function is private, it is still used through the main function call, and the DataWeave Mapping file is also able to import and reuse the dw::core::Strings module.

== Import a Module

When you import a module, all of the functions and variables that the module includes become available to use in the DataWeave body.

In the example below, the module `MyModule` is imported in the header. Thanks to that, the function `myFunc` (that is packaged in that module) can be called, which adds an underscore at the end of the string.

.Example
[source,DataWeave,linenums]
----
%dw 2.0
import modules::MyModule
output application/json
---
MyModule::myFunc("dataweave")
----

.Output
[source,Json,linenums]
----
"dataweave_"
----


In this case, the module that's being imported is stored in the path `modules/MyModule.dwl` and contains the following:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
fun myfunc(name:String) = name ++ "_"
----


== Import an Element from a Module

You can choose to import only a specific element from a module, like a single function. This also helps you avoid typing the module name each time you refer to the element.

.Example
[source,DataWeave,linenums]
----
%dw 2.0
import myFunc from modules::MyModule
output application/json
---
myFunc("dataweave") ++ "name"
----

.Output
[source,Json,linenums]
----
"dataweave_name"
----

In this case, the module that's being imported is stored in the path `modules/MyModule.dwl` and contains the following:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
fun myfunc(name:String) = name ++ "_"
----

You can also invoke an element directly in the DataWeave body, without needing to import it in the header:

.Example
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
modules::MyModule::myFunc("dataweave") ++ "name"
----




== Import All Elements from a Module

As an alternative to importing the module, you can instead import all elements from the module. The advantage of this is that it then saves you from having to type the module name each time you refer to the element.


.Example
[source,DataWeave,linenums]
----
%dw 2.0
import * from modules::MyModule
output application/json
---
myFunc("dataweave") ++ myVar
----

.Output
[source,Json,linenums]
----
"dataweave_name"
----

In this case, the module that's being imported is stored in the path `modules/MyModule.dwl` and contains the following:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
fun myfunc(name:String) = name ++ "_"
var myVar = "Test"
----

== Assign a Local Alias for an Imported Element

You can assign an alternative alias to imported modules or elements from within them to avoid name clashes.

.Example
[source,DataWeave,linenums]
----
%dw 2.0
import myFunc as appendDash, myVar as weaveName from modules::MyModule
var myVar = "Mapping"
output application/json
---
appendDash("dataweave") ++ weaveName ++ "_" ++ myVar
----

.Output
[source,Json,linenums]
----
"dataweave_name_Mapping"
----

You can also apply an alias to an imported module.

.Example
[source,DataWeave,linenums]
----
%dw 2.0
import modules::MyModule as WeaveMod
output application/json
---
WeaveMod::myFunc("dataweave")
----

.Output
[source,Json,linenums]
----
"dataweave_"
----


In this case, the module that's being imported is stored in the path `modules/MyModule.dwl` and contains the following:

.Example Module
[source,DataWeave,linenums]
----
%dw 2.0
fun myfunc(name:String) = name ++ "_"
var myVar = "Test"
----




== See Also

* link:dataweave-create-module[To Create DataWeave Modules]
