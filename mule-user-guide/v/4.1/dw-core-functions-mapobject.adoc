= mapObject

//* <<mapobject1>>
//* <<mapobject2>>


[[mapobject1]]
===== mapObject({ (K)?: V }, (V, K, Number) -> Object): Object

Similar to Map, but instead of processing only the values of an object, it processes both keys and values as a tuple. Also instead of returning an array with the results of processing these values through the lambda, it returns an object, which consists of a list of the key:value pairs that result from processing both key and value of the object through the lambda.

The lambda is invoked with three parameters: *value*, *key* and *index*.
If these parameters are not named, the value is defined by default as *$*, the key *$$* and the index *$$$*.

.Transform
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
var conversionRate=13.45
---
priceList: payload.prices mapObject (
  '$$':{
    dollars: $,
    localCurrency: $ * conversionRate
  }
)
----


.Input
[source,xml,linenums]
----
<prices>
    <basic>9.99</basic>
    <premium>53</premium>
    <vip>398.99</vip>
</prices>
----

.Output
[source,json,linenums]
----
{
  "priceList": {
    "basic": {
      "dollars": "9.99",
      "localCurrency": 134.3655
    },
    "premium": {
      "dollars": "53",
      "localCurrency": 712.85
    },
    "vip": {
      "dollars": "398.99",
      "localCurrency": 5366.4155
    }
  }
}
----


[TIP]
Note that when you use a parameter to populate one of the keys of your output, as with the case of $$ in this example, you must either enclose it in quote marks or brackets. '$$' or ($$) are both equally valid.

In the example above, as key and value are not defined, they're identified by the placeholders *$$* and *$*.
For each key:value pair in the input, the key is preserved and the value becomes an object with two properties:
one of these is the original value, the other is the result of multiplying this value by a constant that is defined as a directive in the header.

The mapping below performs exactly the same transform, but it defines custom names for the properties of the operation, instead of using $ and $$. Here, 'category' is defined as referring to the original key in the object, and 'money' to the value in that key.

.Transform
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
var conversionRate=13.45
---
priceList: payload.prices mapObject ((money, category, index) ->
  '$category':{
    dollars: money,
    localCurrency: money * conversionRate
  }
)
----

[TIP]
Note that when you use a parameter to populate one of the keys of your output, as with the case of *category* in this example, you must either enclose it in brackets or enclose it in quote marks adding a $ to it, otherwise the name of the property is taken as a literal string. '$category' or (category) are both equally valid.


[[mapobject2]]
===== mapObject(Null, (Any, Any, Number) -> Any): Null

Helper function that allows mapObject to work with null values

