= map

<<map1>> |
<<map2>> |


[[map1]]
== map(Array<T>, (item: T, index: Number) -> R): Array<R>


Returns an array that is the result of applying a transformation function (lambda) to each of the elements.
The lambda is invoked with two parameters: *value* and the *index*.
If these parameters are not named, the index is defined by default as *$$* and the value as *$*.

.Transform
[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
users: ["john", "peter", "matt"] map  upper($)
----

.Output
[source,json,linenums]
----
{
 "users": [
   "JOHN",
   "PETER",
   "MATT"
  ]
}
----

In the following example, custom names are defined for the index and value parameters of the map operation, and then both are used to construct the returned value.
In this case, value is defined as *firstName* and its index in the array is defined as *position*.

.Transform
[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
users: ["john", "peter", "matt"] map ((firstName, position) -> position ++ ":" ++ upper(firstName))
----

.Output
[source,json,linenums]
----
{
  "users": [
    "0:JOHN",
    "1:PETER",
    "2:MATT"
  ]
}
----


[[map2]]
== map(Null, (Nothing, Nothing) -> Boolean): Null

Helper function that allows *map* to work with null values

