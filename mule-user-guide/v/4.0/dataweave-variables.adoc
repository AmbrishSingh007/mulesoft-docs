= About DataWeave Variables
:keywords: studio, anypoint, esb, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

[[dataweave_variables]]
So I agree the first variables page is not great.  DataWeave is a functional programming language where variables behave just like functions. Variables can be assigned to constants or other expressions, including lambda expressions that allow parameters to be passed in to the expression and used.  

Examples of DataWeave variable assignments include:

[source,xml, linenums]
----
%dw 2.0
output application/json
var msg = "hello"

var msg2 = (x = "ignore") -> "hello"

var toUpper = (aString) -> upper(aString)

var combined = (function, msg="universe") -> function(msg ++ " world")
---
[
   msg: msg,
   msg2: msg2(),
   toUpper: toUpper(msg),
   combined: combined(toUpper, msg="world"),
   combined2: combined((x) -> lower(x) ++ " today", msg)
]
----

results in:

[source,xml, linenums]
----
[
  {
    "msg": "hello"
  },
  {
    "msg2": "hello"
  },
  {
    "toUpper": "HELLO"
  },
  {
    "combined": "HELLO WORLD"
  },
  {
    "combined2": "hello world today"
  }
]
----

In these examples, the msg var is assigned to a constant. 

msg2 is assigned to an expression that maps to a constant, so msg2() behaves the same way as msg, without the (). 

The toUpper var maps an input parameter to the DataWeave upper function, and so is used like a more traditional function in other languages as `toUpper(aString)`. 

The combined var accepts a function name as an input parameter, then applies that function name to the second msg argument. The second msg argument is optional, because a default value “universe” is specified. 

The result of the expression `combined(toUpper, " world")` is to apply the toUpper function defined earlier in the header to the msg parameter value “world”. The supplied second argument “world” overrides the `msg="universe"` default value, the the result is “HELLO WORLD”. 

The final combined2 key is the expression ```combined((x) -> lower(x) ++ " today", msg)```. Now instead of passing in an already defined function, you can define the lambda expression “anonymously”, without any name. Lambda expressions allow you to reuse more generalized expressions in different contexts. In this example the combined key used an already defined variable named toUpper, while the combined2 key defines the expression “in-place” at the time the function is called. So in this example instead of converting the string “world” to “HELLO WORLD”, it is instead converted to “hello world today”. 

Note: These types of “in-place” expressions are called closures in other languages like JavaScript. 


DataWeave includes syntactic sugar to access lambda expressions assigned to a variable as functions. To do this, you replace the `var` directive with the `fun` directive, and replace the arrow `->` in the lambda expression with and `=` sign. You also move the lambda expression arguments next to the function name so the syntax looks like a function declaration in other procedural programming languages. This allows you to use variables as if they are functions. So the previous example can be equivalently written as:

[source,xml, linenums]
----

%dw 2.0
output application/json
var msg = "hello"

fun msg2(optParm = "ignore") = "hello"

fun toUpper(aString) = upper(aString)

fun combined(function, msg="universe") = function(msg ++ " world")
---
[
   msg: msg,
   msg2: msg2(),
   toUpper: toUpper(msg),
   combined: combined(toUpper, msg),
   combined2: combined((x) -> lower(x) ++ " today", msg)
]
----

This gives the exact same results: 


[source,xml, linenums]
----
[
{
“msg”: “hello”
},
{
“msg2”: “hello”
},
{
“toUpper”: “HELLO”
},
{
“combined”: “HELLO WORLD”
},
{
“combined2”: “hello world today”
}
]
----

The important distinction in DataWeave though, is that functions are variables, and every function is just a syntactical renaming of the underlying `var` syntax, which allows you to pass function names or lambda expressions as arguments to other functions. The fun syntax allows you to still access the powerful functional programming aspects of DataWeave while also being able to write simpler expressions as function calls you may be more familiar with. 

Also notice that DataWeave variables (and functions) can specify any number of optional arguments by providing default values, so long as all those optional arguments are last in the argument list. 


=== Variable Scopes

You can initialize and use both global and local variables in DataWeave scripts.

* Global variables are initialized in the header of the DataWeave script and can be referenced by name from anywhere in the body of a DataWeave script.
+
The header of a DataWeave script accepts a `var` directive that initializes a variable, for example: `var language='Español'`. You can declare multiple global variables on separate lines in the header.
+
* Local variables are initialized in the body of the DataWeave script and can be referenced by name only from within the scope of the expression where they are initialized.
+
The syntax for initializing a local variable looks like this:
`using (<variable-name> = <expression>)`
+
It must use the keyword `using`, and it must use the `var` syntax, rather than the `fun` syntax. 
+

You can combine several local variable definitions as a comma separated list inside the using function. For example: `using (firstName='Annie', lastName='Point')`


[[example_global_variable]]
=== Example: Global DataWeave Variables

This example initializes the `language` variable in the header and inserts the constant value `Español` in the output `language` element.

.Transform
[source, dataweave, linenums]
----
%dw 2.0
output application/xml
var language='Español'
---
{
  document: {
    language: language,
    text: "Hola mundo"
  }
}
----

.Output
[source,xml, linenums]
----
<?xml version="1.0" encoding="UTF-8"?>
<document>
  <language>Español</language>
  <text>Hola Mundo</text>
</document>
----

[[example_local_variable]]
=== Examples: Local DataWeave Variables

To initialize local variables, you can use either literal expressions, variable reference expressions, or functional expressions. These expressions can reference any other local variables within their scope or any input or global variables.

You can only reference a local variable by name from within the scope of the expression that initializes it. The declaration can be prepended to any literal expression. The literal delimits the scope of the variable, so you cannot reference any variable outside of its scope.

The examples that follow show initialization of local variables used in literal expressions. The syntax is the same

.Example: Scoped to Simple Value
[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
using (x = 2) 3 + x
----

The result is `5`.

.Example: Scoped to an Array Literal
[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
using (x = 2) [1, x, 3]
----

The result is `[ 1, 2, 3]`

.Example: Scoped to Object literal
Here, the reference to `user` is valid because it is within the object `person`.

[source, dataweave, linenums]
----
%dw 2.0
output application/xml
---
{
  person: using (user='Greg', gender='male') {
    name: user,
    gender: gender
  }
}
----

.Output
[source, xml, linenums]
----
<?xml version='1.0' encoding='UTF-8'?>
<person>
  <name>Greg</name>
  <gender>male</gender>
</person>
----

.Example: Invalid Reference that is Outside the Scope
This example produces an error because `gender` is referenced from outside the scope of `person`, where `gender` is initialized.

[source, dataweave, linenums]
----
%dw 2.0
output application/xml
---
entry: using (firstName = "Annie", lastName = "Point") {
  person: using (user = firstName, gender = "male") {
    name: user,
    gender: gender
  },
  sn: lastName,
  gen: gender
}
----

The invalid example returns this error:
`Unable to resolve reference of gender.`

.Example: Scoped to a Global Variable and to a Function
[source, dataweave, linenums]
----
%dw 2.0
output application/json
var myGlobalVar = 2
---
{
  examples: {
    // x is a global variable that is referenced by a literal expression.
    'ex_a': using (x = myGlobalVar) 3 + x,
    // y is an expression that coerces a Boolean into a string,  then referenced by the upper function.
    'ex_b': using (y = true as String) upper(y ++ ' as a string'),
    // z is a function expression that is referenced by a literal object.
    'ex_c': using (z = ["a", "b", "c"] map upper($)) {mapping : z}
  }
}
----

.Output
[source, json, linenums]
----
{
  "examples": {
    "ex_a": 5,
    "ex_b": "TRUE AS A STRING",
    "ex_c": {
      "mapping": [
        "A",
        "B",
        "C"
      ]
    }
  }
}
----
